buildscript {
	ext {
		springBootVersion = '1.5.3.RELEASE'
		springPlatformVersion = 'Brussels-SR2'
        camelVersion = '2.18.3'
	}
	repositories {
		mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0"
	}
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: "com.palantir.docker"
apply plugin: "com.palantir.docker-run"


version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
    maven {
        url "http://oss.jfrog.org/simple/oss-snapshot-local"
    }
}

dependencyManagement {
    imports {
		// camel-parent has some outdated Spring dependencies, so place it first, because the last BOM wins
		mavenBom "org.apache.camel:camel-parent:${camelVersion}"
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
//        mavenBom "io.spring.platform:platform-bom:${springPlatformVersion}"
    }
}


dependencies {
	compile "org.springframework.boot:spring-boot-starter-web"

	compile "org.apache.camel:camel-jackson-starter"
	compile "org.apache.camel:camel-servlet-starter"
	compile "org.apache.camel:camel-spring-boot-starter"
	compile "org.apache.camel:camel-swagger-java-starter"
	compile "org.apache.camel:camel-swagger"

	// springfox-swagger-ui is a Swagger2-compatible replacement for older and abandoned swagger-springmvc library
	compile "io.springfox:springfox-swagger2:2.7.0-SNAPSHOT"
	compile "io.springfox:springfox-swagger-ui:2.7.0-SNAPSHOT"

	compile "org.projectlombok:lombok:1.16.16"

	testCompile "com.jayway.restassured:rest-assured:2.9.0"
    testCompile "org.apache.camel:camel-test"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

jar {
	archiveName "${project.name}.jar"
}

docker {
	name "test/sprinc-camel-rest-demo:${project.version}"
	files "build/libs/${project.name}.jar"
	dependsOn jar, bootRepackage
}

dockerRun {
	name 'spring-camel-test'
	image "test/sprinc-camel-rest-demo:${project.version}"
	ports "8080:8080"
	daemonize true
}

